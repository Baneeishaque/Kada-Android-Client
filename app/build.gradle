plugins {

    id 'com.android.application'
}

android {

    compileSdk 30
    buildToolsVersion "30.0.3"

    signingConfigs {

        release {

            keyAlias 'snake'
            keyPassword '9895204814'
            storeFile file('../keystore.jks')
            storePassword '9895204814'
        }
    }

    defaultConfig {

        applicationId "ndk.kada"

        minSdk 22
        targetSdk 30

        versionCode 1
        versionName "1.0"

        useLibrary 'org.apache.http.legacy'

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {

        release {

            signingConfig signingConfigs.release
//            minifyEnabled true
//            shrinkResources true
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        staging {

            initWith release
            debuggable true
        }
    }

    compileOptions {

        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    flavorDimensions "relevence"
    productFlavors {

        // When building a variant that uses this flavor, the following configurations override those in the defaultConfig block.

        development {

            dimension "relevence"

            // To avoid using legacy multidex when building from the command line, set minSdkVersion to 21 or higher. When using Android Studio 2.3 or higher, the build automatically avoids legacy multidex when deploying to a device running API level 21 or higherâ€”regardless of what you set as your minSdkVersion.
//            minSdkVersion 21

            // The following configuration limits the usage of mdpi screen-density resources.
            resConfigs "mdpi"
        }

        production {

            dimension "relevence"
            // If you've configured the defaultConfig block for the release version of your app, you can leave this block empty and Gradle uses configurations in the defaultConfig block instead. You still need to create this flavor. Otherwise, all variants use the "development" flavor configurations.
        }
    }

    variantFilter { variant ->

        def names = variant.flavors*.name

        // To check for a certain build type, use variant.buildType.name == "<buildType>"
        if (names.contains("development") && names.contains("release")) {

            // Gradle ignores any variants that satisfy the conditions above.
            setIgnore(true)
        }

        if (names.contains("production") && names.contains("debug")) {

            // Gradle ignores any variants that satisfy the conditions above.
            setIgnore(true)
        }

        if (names.contains("development") && names.contains("staging")) {

            // Gradle ignores any variants that satisfy the conditions above.
            setIgnore(true)
        }
    }
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
    buildFeatures {
        viewBinding true
    }
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'

    implementation 'com.mikhaellopez:circleview:1.4.0'
    implementation 'com.github.mukeshsolanki:android-otpview-pinview:2.1.2'
    implementation 'com.github.pratikbutani:MultiSelectSpinner:1.0.1'
    implementation 'com.github.Drjacky:ImagePicker:2.1.12'
    implementation 'com.makeramen:roundedimageview:2.3.0'
    implementation 'com.squareup.picasso:picasso:2.71828'

    implementation project(path: ':Utils14:common14')
    implementation project(path: ':Utils1:common1')

    implementation project(path: ':Resources:common')

    implementation project(path: ':Utils16:common16')
    implementation 'org.javatuples:javatuples:1.2'

    implementation project(path: ':Utils19:common19')

    implementation project(path: ':DebugUtils:common')

    implementation 'com.google.android.gms:play-services-maps:17.0.1'

    testImplementation 'junit:junit:4.13.2'

    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}
